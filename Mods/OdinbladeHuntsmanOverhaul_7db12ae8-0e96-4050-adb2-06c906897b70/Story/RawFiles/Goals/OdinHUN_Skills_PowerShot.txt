Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION Power Shot damage
//Character uses less than 4AP
IF
CharacterUsedSkill(_Character, "Projectile_OdinHUN_PowerShot", _, _)
AND
NOT DB_OBHUN_PowerShot(_Character, _)
AND
NRD_CharacterGetStatInt(_Character, "CurrentAP", _IntAP)
AND
_IntAP <= 4
THEN
PROC_OBHUN_InstigatePowerShot(_Character, _IntAP);

//Character uses 4AP
IF
CharacterUsedSkill(_Character, "Projectile_OdinHUN_PowerShot", _, _)
AND
NOT DB_OBHUN_PowerShot(_Character, _)
AND
NRD_CharacterGetStatInt(_Character, "CurrentAP", _IntAP)
AND
_IntAP > 4
THEN
PROC_OBHUN_InstigatePowerShot(_Character, 4);

PROC
PROC_OBHUN_InstigatePowerShot((CHARACTERGUID)_Character, (INTEGER)_IntAP)
AND
Real(_IntAP, _RealAP)
THEN
DB_OBHUN_PowerShot((GUIDSTRING)_Character, _RealAP);
SetStoryEvent(_Character, "OBHUN_USE_POWERSHOT");
ObjectSetFlag(_Character, "OdinHUN_POWERSHOT");
ProcObjectTimer((CHARACTERGUID)_Character, "OBHUN_END_POWERSHOT", 1250);

PROC
ProcObjectTimerFinished(_Character, "OBHUN_END_POWERSHOT")
AND
DB_OBHUN_PowerShot(_Character, _CharAP)
THEN
NOT DB_OBHUN_PowerShot(_Character, _CharAP);
ObjectClearFlag(_Character, "OdinHUN_POWERSHOT");

//Perform Power Shot skill
IF
NRD_OnPrepareHit(_Target, _Dealer, _, _HitHandle)
AND
ObjectGetFlag(_Dealer, "OdinHUN_POWERSHOT", 1)
AND
DB_OBHUN_PowerShot(_Dealer, _UsedAP)
AND
DB_OBHUN_DamageType(_PreDamageType)
AND
NRD_HitGetDamage(_HitHandle, _PreDamageType, _Damage)
AND
_Damage > 0
AND
Real(_Damage, _RealDamage)
AND
RealProduct(_RealDamage, _UsedAP, _RealAddDamage)
AND
Integer(_RealAddDamage, _FinalDamage)
AND
QRY_OBHUN_Determine_Type(_Dealer, _PreDamageType)
AND
DB_OBHUN_DeterminedType_Temp(_Dealer, _DamageType)
THEN
NRD_HitClearDamage(_HitHandle, _PreDamageType);
NRD_HitAddDamage(_HitHandle, _DamageType, _FinalDamage);
NOT DB_OBHUN_DeterminedType_Temp(_Dealer, _DamageType);
//END_REGION
EXITSECTION

ENDEXITSECTION
