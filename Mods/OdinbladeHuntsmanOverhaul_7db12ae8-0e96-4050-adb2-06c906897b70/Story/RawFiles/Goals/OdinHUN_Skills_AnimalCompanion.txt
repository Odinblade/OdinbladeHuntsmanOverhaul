Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_OBHUN_AnimalCompanion_Template("OdinHUN_AC_Owl", "065038d4-441e-44b7-9d59-e63cb25d24c4", "710ec76f-0716-48a5-9db2-cb2c93e686e8");
DB_OBHUN_AnimalCompanion_Template("OdinHUN_AC_Bear", "4b40c81d-dd6b-4618-a4b3-fc709ceee764", "134e04ec-404a-4f8a-bb06-78fe397bb670");
DB_OBHUN_AnimalCompanion_Template("OdinHUN_AC_Deer", "fa245b33-29b5-4a50-b6f2-87d6c83717f0", "0c09b07d-c535-4083-8be0-a31f27548163");

GlobalSetFlag("OdinHUN_Fix_V1_DB");

KBSECTION
IF
SavegameLoaded(_,_,_,_)
AND
NOT DB_GlobalFlag("OdinHUN_Fix_V1_DB")
THEN
GlobalSetFlag("OdinHUN_Fix_V1_DB");
NRD_DebugLog("[OdinHUN:Story] Save fix: Applying...");
PROC_OBHUN_Fix_Savegame_DBs();

PROC
PROC_OBHUN_Fix_Savegame_DBs()
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
THEN
NOT DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon);
PROC_OBHUN_ClearAnimalCompanion((CHARACTERGUID)_Owner);
NRD_DebugLog("[OdinHUN:Story] Save fix: Cleared one dead DB_OBHUN_AnimalCompanion_Instance entry.");

//REGION Skill activation
// Gives the player the Dismiss Animal Companion innate skill
IF
SkillActivated(_Owner, "Target_OdinHUN_Summon_AnimalCompanion")
AND
DB_CurrentLevel(_Level)
AND
IsCharacterCreationLevel(_Level, 0)
THEN
CharacterAddSkill((CHARACTERGUID)_Owner, "Shout_OdinHUN_Dismiss_AnimalCompanion", 0);

// Removes the Dismiss Animal Companion innate skill when the user deactivates the Animal Companion skill
IF
SkillDeactivated(_Owner, "Target_OdinHUN_Summon_AnimalCompanion")
THEN
CharacterRemoveSkill((CHARACTERGUID)_Owner, "Shout_OdinHUN_Dismiss_AnimalCompanion");
PROC_OBHUN_ClearAnimalCompanion(_Owner);

//Adds the Dismiss Animal Companion skill when they begin the game after Character Creation
IF
GameStarted(_, _)
AND
DB_IsPlayer(_Player)
AND
CharacterHasSkill(_Player, "Target_OdinHUN_Summon_AnimalCompanion", 1)
AND
CharacterHasSkill(_Player, "Shout_OdinHUN_Dismiss_AnimalCompanion", 0)
THEN
CharacterAddSkill((CHARACTERGUID)_Player, "Shout_OdinHUN_Dismiss_AnimalCompanion", 0);
//END_REGION

//REGION Summoning logic
// Initiate dialog window when player uses the Animal Companion skill
IF
CharacterUsedSkillAtPosition(_Owner, _X, _Y, _Z, "Target_OdinHUN_Summon_AnimalCompanion", _, _)
AND
StartDialog_Internal("OdinHUN_Dialog_AnimalCompanion", 1, _Owner, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, NULL_00000000-0000-0000-0000-000000000000, _)
AND
PlayLoopEffect(_Owner, "OdinHUN_Prepare_Dialog_02", "Dummy_Root", _FxHandle)
THEN
PROC_GLO_CharacterAnimationStart(_Owner, "skill_prepare_totem_02_loop", 1, 1);
DB_OBHUN_AnimalCompanion_Prepare(_Owner, _X, _Y, _Z, _FxHandle);

// When Summoning < 10, prepare to summon an Animal Companion
IF
ObjectFlagSet(_Flag, _Owner, _)
AND
DB_OBHUN_AnimalCompanion_Template(_Flag, _SmallTemplate, _SwoleTemplate)
AND
DB_OBHUN_AnimalCompanion_Prepare((CHARACTERGUID)_Owner, _X, _Y, _Z, _FxHandle)
AND
CharacterGetAbility(_Owner, "Summoning", _SumPoints)
AND
_SumPoints < 10
THEN
StopLoopEffect(_FxHandle);
PROC_OBHUN_ClearAnimalCompanion(_Owner);
// RemoveStatus(_Owner, "OdinHUN_NATURALBOND");
ObjectClearFlag(_Owner, _Flag);
PROC_OBHUN_AnimalCompanion_Summon(_Owner, _SmallTemplate);

// When Summoning => 10, prepare to summon an enhanced Animal Companion
IF
ObjectFlagSet(_Flag, _Owner, _)
AND
DB_OBHUN_AnimalCompanion_Template(_Flag, _SmallTemplate, _SwoleTemplate)
AND
DB_OBHUN_AnimalCompanion_Prepare((CHARACTERGUID)_Owner, _X, _Y, _Z, _FxHandle)
AND
CharacterGetAbility(_Owner, "Summoning", _SumPoints)
AND
_SumPoints > 9
THEN
StopLoopEffect(_FxHandle);
PROC_OBHUN_ClearAnimalCompanion(_Owner);
// RemoveStatus(_Owner, "OdinHUN_NATURALBOND");
ObjectClearFlag(_Owner, _Flag);
PROC_OBHUN_AnimalCompanion_Summon(_Owner, _SwoleTemplate);

// Summon the Animal Companion and play VFX/animation
PROC
PROC_OBHUN_AnimalCompanion_Summon((GUIDSTRING)_Owner, (STRING)_SwoleTemplate)
AND
DB_OBHUN_AnimalCompanion_Prepare((CHARACTERGUID)_Owner, _X, _Y, _Z, _FxHandle)
AND
CharacterGetLevel(_Owner, _Level)
AND
NRD_Summon(_Owner, _SwoleTemplate, _X, _Y, _Z, -1.0, _Level, 0, 0, _Summon)
AND
NRD_CharacterGetInt(_Summon, "Totem", _IsTotem)
AND
IntegertoString(_IsTotem, _IsTotemString)
THEN
SetTag(_Owner, "ODINHUN_ANIMALCOMPANION");
PlayEffect(_Summon, "RS3_FX_Skills_Totem_Impact_Summon_Poison_01");
CharacterSetAnimationOverride(_Owner, "");
ProcObjectTimer(_Owner, "OdinHUN_AC_WHISTLE_ANIMATION", 150);
ProcObjectTimer(_Owner, "OdinHUN_AC_WHISTLE_SFX", 500);
PROC_OBHUN_NaturalBond_Apply(_Summon, _Owner);
NOT DB_OBHUN_AnimalCompanion_Prepare(_Owner, _X, _Y, _Z, _FxHandle);
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon);
// NRD_DebugLog("[OdinHUN:Story] Animal Companion successfully summoned");

PROC
ProcObjectTimerFinished(_Owner, "OdinHUN_AC_WHISTLE_ANIMATION")
THEN
PlayAnimation(_Owner, "Whistle_01");

PROC
ProcObjectTimerFinished(_Owner, "OdinHUN_AC_WHISTLE_SFX")
THEN
PlaySound(_Owner, "Humans_Hero_Female_ABC_Custom_Whistle_01");
//END_REGION

//REGION Animal Companion dismissal logic
// Initiate dismissal when player uses the Dismiss Animal Companion skill
IF
CharacterUsedSkill(_Owner, "Shout_OdinHUN_Dismiss_AnimalCompanion", _, _)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
THEN
PROC_OBHUN_ClearAnimalCompanion(_Owner);

// Remove the Animal Companion and do some outro visuals to mask lack of death animations
PROC
PROC_OBHUN_ClearAnimalCompanion((CHARACTERGUID)_Owner)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
AND
GetPosition((GUIDSTRING)_Summon, _X, _Y, _Z)
THEN
ClearTag(_Owner, "ODINHUN_ANIMALCOMPANION");
// NRD_DebugLog("[OdinHUN:Story] PROC_OBHUN_ClearAnimalCompanion Tag removed");
NOT DB_OBHUN_AnimalCompanion_Instance(_Owner, (CHARACTERGUID)_Summon);
PlayEffectAtPosition("RS3_FX_Skills_Totem_Impact_Summon_Poison_01", _X, _Y, _Z);
SetVisible(_Summon, 0);
// RemoveStatus(_Owner, "OdinHUN_NATURALBOND");
CharacterDie((CHARACTERGUID)_Summon, 0, "None");

// When player summons a new AC, play VFX and hide old summon before it is removed (death animation workaround)
IF
CharacterStatusApplied(_NewSummon, "SUMMONING", _)
AND
NRD_CharacterGetInt(_NewSummon, "Totem", 0)
AND
CharacterGetOwner(_NewSummon, _Owner)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
AND
_NewSummon != _Summon
AND
HasActiveStatus(_Summon, "SUMMONING", 0)
AND
IsTagged(_NewSummon, "ODINHUN_IS_ANIMALCOMPANION", 1)
AND
IsTagged(_Summon, "ODINHUN_IS_ANIMALCOMPANION", 1)
AND
GetPosition(_Summon, _X, _Y, _Z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Totem_Impact_Summon_Poison_01", _X, _Y, _Z);
SetVisible(_Summon, 0);
// ClearTag(_Owner, "ODINHUN_ANIMALCOMPANION");
CharacterSetHitpointsPercentage(_Summon, 0.0);
// NRD_DebugLog("[OdinHUN:Story] PROC_OBHUN_ClearFlag_AnimalCompanion Tag removed");

// AC died and cleanup is needed
IF
CharacterDied(_Summon)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
AND
IsTagged(_Summon, "ODINHUN_IS_ANIMALCOMPANION", 1)
AND
GetPosition(_Summon, _X, _Y, _Z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Totem_Impact_Summon_Poison_01", _X, _Y, _Z);
ClearTag(_Owner, "ODINHUN_ANIMALCOMPANION");
SetVisible(_Summon, 0);
NOT DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon);

//AC died and cleanup is not needed
IF
CharacterDied(_Summon)
AND
NOT DB_OBHUN_AnimalCompanion_Instance(_, _Summon)
AND
IsTagged(_Summon, "ODINHUN_IS_ANIMALCOMPANION", 1)
AND
CharacterGetOwner(_Summon, _Owner)
AND
GetPosition(_Summon, _X, _Y, _Z)
THEN
// PlayEffectAtPosition("RS3_FX_Skills_Totem_Impact_Summon_Poison_01", _X, _Y, _Z);
// ClearTag(_Owner, "ODINHUN_ANIMALCOMPANION");
SetVisible(_Summon, 0);
// NOT DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon);
//END_REGION

//REGION Other interactions
// Logic for the Camouflage companion skill
IF
CharacterStatusApplied(_Owner, "OdinHUN_PROC_CAMOUFLAGE", _)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
THEN
ApplyStatus(_Owner, "INVISIBLE", 6.0, 1, _Owner);
ApplyStatus(_Owner, "OdinHUN_CAMOUFLAGE_HEAL", 0.0, 1, _Owner);
ApplyStatus(_Summon, "INVISIBLE", 6.0, 1, _Owner);
ApplyStatus(_Summon, "OdinHUN_CAMOUFLAGE_HEAL", 0.0, 1, _Owner);
PlayEffect(_Summon, "OdinHUN_Impact_02");
//END_REGION

//REGION Ladder malarchy
// Player has climbed a ladder, queue a teleport for AC
IF
CharacterStatusRemoved(_Owner, "CLIMBING", _)
AND
CharacterIsInCombat(_Owner, 0)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
THEN
ProcObjectTimer(_Owner, "OdinHUN_LADDER", 300);

// Teleport the AC to the player
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Owner, "OdinHUN_LADDER")
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
THEN
TeleportTo(_Summon, _Owner);

// If player starts climbing a ladder, cancel queued AC teleports
IF
CharacterStatusApplied(_Owner, "CLIMBING", _)
AND
CharacterIsInCombat(_Owner, 0)
THEN
ProcObjectTimerCancel(_Owner, "OdinHUN_LADDER");
//END_REGION

//REGION Waypoint malarchy
// Should work anyway, but just in case
IF
CharacterTeleportToWaypoint(_Owner, _)
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
AND
CharacterIsInCombat(_Summon, 0)
THEN
TeleportTo(_Summon, _Owner);

// When another party member teleports a chained group, iterate each party member
IF
CharacterTeleportToWaypoint(_Teleporter, _)
THEN
DB_OBHUN_TeleportParty(_Teleporter);
IterateParty(_Teleporter, "OdinHUN_TP_Party");
ProcObjectTimer(_Teleporter, "OdinHUN_TP_Party_Timeout", 200);

// If party member is grouped with a teleporter, teleport ACs of the party member too
IF
StoryEvent((CHARACTERGUID)_Owner, "OdinHUN_TP_Party")
AND
DB_OBHUN_AnimalCompanion_Instance(_Owner, _Summon)
AND
DB_OBHUN_TeleportParty(_Teleporter)
AND
CharactersAreGrouped(_Teleporter, _Owner, 1)
AND
CharacterIsInCombat(_Summon, 0)
THEN
TeleportTo(_Summon, _Owner);

// Cleanup
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Teleporter, "OdinHUN_TP_Party_Timeout")
AND
DB_OBHUN_TeleportParty(_Teleporter)
THEN
NOT DB_OBHUN_TeleportParty(_Teleporter);
//END_REGION
EXITSECTION

ENDEXITSECTION
