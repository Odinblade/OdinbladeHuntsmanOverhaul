Version 1
SubGoalCombiner SGC_AND
INITSECTION
// DB_OBHUN_SA_ExcludedWeapon("WPN_Riftshot_BonusUniques_23f4ab46-309c-4bdf-b788-77412b4fcef1");

KBSECTION
IF
NRD_OnPrepareHit(_Target, _Dealer, _Damage, _HitHandle)
AND
HasActiveStatus(_Dealer, "OdinHUN_SEEKINGARROWS", 1)
AND
NOT DB_OBHUN_SeekingArrows(_Target, _Dealer)
AND
DB_OBHUN_EA_HitType((INTEGER)_CompatibleHitType)
AND
NRD_HitGetInt(_HitHandle, "FromShacklesOfPain", 0)
AND
NRD_HitGetInt(_HitHandle, "Reflection", 0)
AND
NRD_HitGetInt(_HitHandle, "HitType", _CompatibleHitType)
AND
GetDistanceTo(_Dealer, _Target, _Distance)
AND
_Distance >= 6
THEN
DB_OBHUN_SeekingArrows(_Target, (GUIDSTRING)_Dealer);

IF
NRD_OnHit(_Target, _Dealer, _Damage, _HitHandle)
AND
HasActiveStatus(_Dealer, "OdinHUN_SEEKINGARROWS", 1)
AND
DB_OBHUN_SeekingArrows(_Target, _Dealer)
AND
NRD_StatusGetString(_Target, _HitHandle, "SkillId", _SkillID)
AND
NRD_StatusGetString(_Target, _HitHandle, "DamageType", _DamageType)
AND
_DamageType != "Shadow" // Fix for incompatibilities with modded bows which deal part Shadow-damage as an on-hit status
AND
_SkillID != "Projectile_OdinHUN_SeekingArrows_Shot"
AND
GetPosition(_Dealer, _dX, _dY, _dZ)
AND
GetPosition(_Target, _tX, _tY, _tZ)
AND
RealSum(_dY, 2.0, _Y)
AND
Real(_Damage, _RealDamage)
AND
RealProduct(_RealDamage, 0.3, _RealSADamage)
AND
Integer(_RealSADamage, _SADamage)
THEN
PROC_OBHUN_SeekingArrows_Launch(_Dealer, _Target, _dX, _Y, _dZ,_tX, _tY, _dZ);

// Launch Seeking Arrows, dealing default damage (for skills with unique damage calculations)
PROC
PROC_OBHUN_SeekingArrows_Launch((GUIDSTRING)_Dealer, (GUIDSTRING)_Target, (REAL)_dX, (REAL)_Y, (REAL)_dZ, (REAL)_tX, (REAL)_tY, (REAL)_tZ)
THEN
NRD_ProjectilePrepareLaunch();
NRD_ProjectileSetString("SkillId", "Projectile_OdinHUN_SeekingArrows_Shot");
NRD_ProjectileSetVector3("SourcePosition", _dX, _Y, _dZ);
NRD_ProjectileSetVector3("TargetPosition", _tX, _tY, _tZ);
NRD_ProjectileSetGuidString("Caster", _Dealer);
NRD_ProjectileSetGuidString("Target", _Target);
NRD_ProjectileLaunch();
ProcObjectTimer(_Dealer, "OdinHUN_SA_GRACEPERIOD", 1750);
// NRD_DebugLog("[OdinHUN:Story] Seeking Arrow launched");

PROC
ProcObjectTimerFinished(_Dealer, "OdinHUN_SA_GRACEPERIOD")
AND
DB_OBHUN_SeekingArrows(_Target, _Dealer)
THEN
NOT DB_OBHUN_SeekingArrows(_Target, _Dealer);
// NRD_DebugLog("[OdinHUN:Story] Grace period ended");
EXITSECTION

ENDEXITSECTION
